apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    destinationDir rootProject.file("${buildDir}/libs")
}

tasks.build.dependsOn sourceJar, javadocJar

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
            artifact(javadocJar)
            version project.version.replaceAll(/-.*/, '-SNAPSHOT')
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    publish = true
    pkg {
        repo = 'releases'
        name = 'dropwizard-web-security'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/palantir/dropwizard-web-security.git'
        publications = ['bintray']

        githubRepo = 'palantir/dropwizard-web-security'
        githubReleaseNotesFile = 'README.md'
    }
}

bintrayUpload.onlyIf {
    System.env.BINTRAY_USER && System.env.BINTRAY_KEY && project.version ==~ /\d+\.\d+\.\d+/
}

bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'
